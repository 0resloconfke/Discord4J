plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'java'
}

ext {
    // Project constants
    github_org = 'Discord4J'
    project_name = 'Discord4J'
    artifact_group = 'com.discord4j'
    project_version = '3.0.0-SNAPSHOT'
    project_description = 'Java interface for the Discord API'
    project_jdk = '1.8'

    // Dependencies
    reactor_bom_version = 'Bismuth-SR7'
    jackson_version = '2.9.2'
    slf4j_version = '1.7.25'
    jsr305_version = '3.0.2'
    junit_version = '4.12'
    logback_version = '1.2.3'
    autoservice_version = '1.0-rc4'

    /** Function returns a new manifest that can be customized per module */
    defaultManifest = {
        return manifest {
            def git_cmd = 'git describe --tags --always --long'
            def git_proc = git_cmd.execute()
            attributes 'SCM-Revision': git_proc.text.trim()
            attributes 'Timestamp': String.valueOf(System.currentTimeMillis())
            attributes 'Build-Host': InetAddress.localHost.hostName
        }
    }

    /** Used where gradle task evaluation would fail because of an undefined value, even if the task wasn't targeted. */
    defaultBlank = { closure ->
        try {
            closure()
        } catch (MissingPropertyException e) {
            ''
        }
    }
}

allprojects {

    group = artifact_group
    version = project_version

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    dependencyManagement {
        imports {
            mavenBom "io.projectreactor:reactor-bom:$reactor_bom_version"
        }
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://jitpack.io' }
    }

    jar {
        doFirst { manifest = defaultManifest() }
        baseName = 'discord4j-' + baseName
    }

    javadoc {
        options.encoding = 'UTF-8'
        options.tags = [
                "apiNote:a:API Note:",
                "implSpec:a:Implementation Requirements:",
                "implNote:a:Implementation Note:"
        ]
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        baseName = 'discord4j-' + baseName
        classifier = 'javadoc'
        from 'build/docs/javadoc'
        doFirst { manifest = defaultManifest() }
    }

    task sourcesJar(type: Jar) {
        baseName = 'discord4j-' + baseName
        classifier = 'sources'
        from sourceSets.main.allSource
        doFirst { manifest = defaultManifest() }
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    shadowJar.dependsOn(assemble)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-4.5.1-all.zip'
}
